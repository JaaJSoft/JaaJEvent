/*
 * Copyright 2020 JaaJSoft
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id "maven-publish"
}

group 'dev.jaaj'
version '0.1.1'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            pom {
                name = project.name
                description = 'A simple Event library'
                url = 'https://github.com/JaaJSoft/JaaJEvent'
                /*properties = [
                        myProp: "value",
                        "prop.with.dots": "anotherValue"
                ]*/
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/JaaJSoft/JaaJEvent'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv('REPO_USER')
                password System.getenv('REPO_PASSWORD')
            }
            def releasesRepoUrl = "https://nexus.jaaj.dev/repository/maven-releases/"
            def snapshotsRepoUrl = "https://nexus.jaaj.dev/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
